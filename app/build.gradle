apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'org.greenrobot.greendao'

def keystorePropertiesFile = rootProject.file("moviedb.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.moviebox.app"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'MOVIEDB_APIKEY', keystoreProperties['MOVIEDB_APIKEY']
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/greendao'
    }
}

//Setup GreenDao
greendao {
    targetGenDir 'src/main/java'
    schemaVersion 1
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Testing Dependencies
    testImplementation 'junit:junit:4.12'

    // Android runner and rules support
    androidTestImplementation(androidRunner) {
        exclude module: 'support-annotations'
    }
    androidTestImplementation(androidRules) {
        exclude module: 'support-annotations'
    }

    // Espresso support
    androidTestImplementation(espressoDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // add this for intent mocking support
    androidTestImplementation(espressoDependencies.espressoIntents) {
        exclude module: 'support-annotations'
    }

    //Support Dependencies
    implementation supportDependencies.appcompatv7
    implementation supportDependencies.design
    implementation supportDependencies.supportv4
    implementation supportDependencies.recycleview
    implementation supportDependencies.cardview
    implementation supportDependencies.percent
    implementation supportDependencies.palettev7
    implementation supportDependencies.contraintLayout

    //RxJava 2
    implementation rxDependencies.rxJava
    implementation rxDependencies.rxAndroid

    //Retrofit Dependencies
    implementation retrofitDependencies.retrofit
    implementation retrofitDependencies.converterGson
    implementation retrofitDependencies.rxAdapterJava

    //Okhttp Dependencies
    implementation okhttpDependencies.okhttp
    implementation okhttpDependencies.loggingInterceptor

    //Dagger Dependencies
    implementation daggerDependencies.dagger
    implementation daggerDependencies.daggerAndroidSupport
    annotationProcessor daggerDependencies.daggerCompiler

    //Butterknife
    implementation butterKnifeDependencies.butterKnife
    annotationProcessor butterKnifeDependencies.butterKnifeCompiler

    //Bolts
    implementation boltsDependencies.boltsTask
    implementation boltsDependencies.boltsAppLinks

    //Debug compile
    debugImplementation debugDB

    //GreenDao
    implementation greenDao

    //Loading Indicator View
    implementation aviLoadingIndicator

    //Image Loader
    implementation picasso
}
